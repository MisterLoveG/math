buildscript {
    ext.kotlin_version = '1.2.10'

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

group 'MathFront'
version '1.0-SNAPSHOT'


apply plugin: 'kotlin2js'
compileKotlin2Js{
    kotlinOptions.metaInfo = true
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = 'commonjs'
}

apply plugin: "com.moowork.node"
node {
    download = true
}
apply plugin: "kotlin-dce-js"

repositories {
    maven {
        url  "https://dl.bintray.com/kotlin/kotlinx.html"
    }
    maven {
        url  "https://kotlin.bintray.com/kotlin-js-wrappers"
    }
    mavenCentral()
}



dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core-js:0.21'
    compile 'org.jetbrains:kotlin-react:16.2.0-pre.17-kotlin-1.2.0'
    compile 'org.jetbrains:kotlin-react-dom:16.2.0-pre.17-kotlin-1.2.0'
    compile 'org.jetbrains:kotlin-extensions:1.0.0-pre.17-kotlin-1.2.0'
    compile 'org.jetbrains.kotlinx:kotlinx-html:0.6.8'
}


task copyStatic(type: Copy) {
    from "$rootDir/src/main/web"
    into "$buildDir/web"
}

task buildBundle(type: NpmTask, dependsOn: [npmInstall, runDceKotlinJs]) {
    args = ["run", "dist"]
}

task copyKotlinJs(type: Copy, dependsOn: compileKotlin2Js) {
    def workDir = "$buildDir/classes/main/"
    from(workDir) {
        include "*.js"
        include "*.js.map"
    }
    into "$workDir/dependencies"
}

task devBuild(dependsOn: [npmInstall, copyStatic, copyKotlinJs])

assemble.dependsOn buildBundle, copyStatic

afterEvaluate {
    copyKotlinJs.dependsOn copyDependenciesKotlinJs
}
